plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "org.encointer.wallet"
    compileSdk flutter.compileSdkVersion
    // flutter ndk version is too low for some reason.
    // This will probably be fixed by an flutter update.
    ndkVersion "25.1.8937393"

    compileOptions {
        // Flag to enable support for the new language API, see #1165.
        coreLibraryDesugaringEnabled true
        // Older androids ( < API 26) use Java 7
        // Sets Java compatibility to Java 8
        // Needed to run JAVA 8 for backwards compatibility with Androids older than API 26, see #1165.
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        multiDexEnabled true
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "org.encointer.wallet"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-build-configuration.
        minSdkVersion 21
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    signingConfigs {
        play {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            // Avoid null error if file doesn't exist, as gradle evaluates the statements even
            // if the signing config is not used.
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // add a product flavor for fdroid release
    flavorDimensions "deploy"
    productFlavors {
        dev {
            dimension "deploy"
            versionNameSuffix "-dev"
        }
        fdroid {
            dimension "deploy"
//            signingConfig null
            versionNameSuffix ""
        }
        play {
            dimension "deploy"
            signingConfig signingConfigs.play
            versionNameSuffix ""
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation 'com.jakewharton.threetenabp:threetenabp:1.4.6'
    // Fixes bug for newer ( > API 26) android versions
    // Bug: Failed to transform window-java-1.0.0-beta04.jar (androidx.window:window-java:1.0.0-beta04)
    implementation 'androidx.window:window:1.0.0'
    implementation 'androidx.window:window-java:1.0.0'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
}
